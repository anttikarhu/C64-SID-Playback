; ALLOWS ONE TO START THE APPLICATION WITH RUN
; SYS 2064
*=$0801 
         BYTE $0C, $8, $0A, $00, $9E, $20, $32, $30, $36, $34, $00, $00, $00, $00, $00

; ADDRESSES WHERE CERTAIN SID SUBROUTINES ARE FOUND.
; ONE MIGHT LOAD THESE FROM THE SID FILE TOO, BUT IN THE SAKE OF AN EXAMPLE, 
; THESE ARE HARD CODED HERE.
LOAD_ADDR       = $1000 ; LOAD ADDRESS, DEPENDS ON THE SID DATA
INIT_ADDR       = $13C8 ; INIT ADDRESS, DEPENDS ON THE SID DATA
PLAY_ADDR       = $875B ; PLAY ADDRESS, DEPENDS ON THE SID DATA

CIA1IRQ         = $DC0D
RASTERREG       = $D011
IRQRASTER       = $D012
IRQADDRMSB      = $0314
IRQADDRLSB      = $0315
IRQCTRL         = $D01A
IRQFLAG         = $D019
IRQFINISH       = $EA31

INIT    
        LDA #%01111111 ; SWITCH OFF CIA-1 INTERRUPTS
        STA CIA1IRQ

        AND RASTERREG ; CLEAR VIC RASTER REGISTER
        STA RASTERREG

        LDA #250 ; SETUP PLAY INTERRUPT AT RASTER LINE 250 
        STA IRQRASTER
        LDA #<PLAYIRQ
        STA IRQADDRMSB
        LDA #>PLAYIRQ
        STA IRQADDRLSB

        JSR INIT_ADDR ; INIT MUSIC

        LDA #%00000001 ; ENABLE RASTER INTERRUPTS ONLY AFTER SETUP
        STA IRQCTRL

MAIN    JMP MAIN


PLAYIRQ JSR PLAY_ADDR
        ASL IRQFLAG ; RESET IRQ FLAG
        JMP IRQFINISH ; LET MACHINE HANDLE OTHER IRQS

* = LOAD_ADDR
incbin "Boulder_Dash.sid",$7E   ; LOAD SID FILE DATA. DATA STARTS AT '7C' BYTES AFTER THE HEADER.
                                ; BUT WE ALSO NEED TO SKIP THE PART OF THE DATA WHICH
                                ; INDICATES THE LOAD ADDRESS, SO WE SKIP '7E' BYTES.